plugins{
	id 'java'
	id 'application'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'edu.sc.seis.launch4j' version '2.5.1'
}

sourceSets{
	main{
		java{
			srcDirs = ['src']
		}
		resources{
			srcDirs = ['src']
		}
	}
}

javadoc{
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
version = 'v2.4'//Don't forget Main.java
shadowJar.archiveName = 'ImageScaler-' + project.version + '.jar'

repositories{
	//Local cache
	if(project.hasProperty("nexusPublic")){
		maven{
			allowInsecureProtocol = true
			url "$nexusPublic"
		}
	}
	jcenter()
}

dependencies{
	implementation 'dev.roanh.util:util:2.0'
	implementation "com.twelvemonkeys.imageio:imageio-bmp:3.8.1"
	implementation "com.twelvemonkeys.imageio:imageio-jpeg:3.8.1"
	implementation "com.twelvemonkeys.imageio:imageio-tiff:3.8.1"
	implementation "com.twelvemonkeys.imageio:imageio-pnm:3.8.1"
	implementation "com.twelvemonkeys.imageio:imageio-hdr:3.8.1"
	implementation "com.twelvemonkeys.imageio:imageio-iff:3.8.1"
	implementation "com.twelvemonkeys.imageio:imageio-pict:3.8.1"
	implementation "com.twelvemonkeys.imageio:imageio-tga:3.8.1"
	implementation "com.twelvemonkeys.imageio:imageio-icns:3.8.1"
}

application{
	mainClassName = 'dev.roanh.imagescaler.Main'
}

launch4j{
	jarTask = project.tasks.shadowJar
	mainClassName = application.mainClassName
	icon = "${projectDir}/imagescaler.ico"
	jreMinVersion = project.sourceCompatibility
	outfile = project.name + '-' + project.version + '.exe'
	fileDescription = project.name
	version = project.version.substring(1)
	textVersion = project.version.substring(1)
	copyright = "Roan Hofland"
}